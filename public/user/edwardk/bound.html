<!DOCTYPE html>
<html><head><title>Bound - School of Haskell | School of Haskell</title><meta http-equiv="x-ua-compatible" content="IE=9"><meta charset="utf-8"><meta name="viewport" content="width=device-width, initial-scale=1.0"><meta name="google-site-verification" content="Rfxk2TdeMMXEXHgJ2_OO8Rt3hPbQSDao0OXQV_n6z_s" /><link href="//fonts.googleapis.com/css?family=Merriweather" rel="stylesheet" type="text/css"><link href="https://www.schoolofhaskell.com/recent-content/feed" type="application/rss+xml" rel="alternate" title="Recently published content">
<link rel="stylesheet" href="https://www.schoolofhaskell.com/static/combined/UlwEHVcM.css"><style>.social{margin-right:1em}h1{font-family:Merriweather !important}article{font-family:Merriweather !important}article li{font-family:Merriweather !important;line-height:1.6em}h1,h2,h3,h4,h4,h5{font-family:Merriweather !important}.editor p{font-family:Frutiger, "Frutiger Linotype", Univers, Calibri, "Gill Sans", "Gill Sans MT", "Myriad Pro", Myriad, "DejaVu Sans Condensed", "Liberation Sans", "Nimbus Sans L", Tahoma, Geneva, "Helvetica Neue", Helvetica, Arial, sans-serif}.article-footer{border-top:1px solid #ddd;padding-top:1em;margin-top:1em}body{font-size:16px !important}.navbar .navbar-inner{background:#3c3f41}.navbar .brand{font-size:14px}.navbar .brand img{width:6em}.navbar .icon-wrench{margin:0 -0.5em 0 -0.5em;padding:0 1em 0 1em;height:20px}.navbar-inverse .nav .active a{background:#333638}.navbar-inverse .nav > li > a{color:#a7a7a7
}.navbar-inverse .btn-navbar{background:#333638}.navbar-inverse .nav > li a:hover,.navbar-inverse .btn{background:#333537 !important}@media (max-width: 979px) {.icon-wrench{padding-left:1em !important}}.breadcrumb{border-top-left-radius:0;border-top-right-radius:0}h1{margin-top:20px}.breadcrumb-container + .main-content h1{margin-top:0}.footer{padding:30px 0;margin-top:70px;border-top:1px solid #e5e5e5;background-color:#f5f5f5}.footer ul.footer-menu{list-style-type:none}.footer ul.footer-menu li{display:inline-block}.footer ul.footer-menu li + li{margin-left:0.5em}.footer ul.footer-menu li + li:before{margin-right:0.5em;content:"·";color:#999}.footer ul.menu li{display:inline-block}.footer ul.menu li + li{margin-left:0.5em}.footer .copyright{margin-left:25px}.main-content{min-height:100%}.gravatar{width:80px;height:80px;border-radius:2px;background:#eee}.empty-gravatar{width:0}.container{font-family:"Segoe UI", Candara, "Bitstream Vera Sans", "DejaVu Sans", "Bitstream Vera Sans", "Trebuchet MS", Verdana, "Verdana Ref", sans-serif}.container h1,.container h2,.container h4,.container h4,.container h5{font-family:"Segoe UI", Candara, "Bitstream Vera Sans", "DejaVu Sans", "Bitstream Vera Sans", "Trebuchet MS", Verdana, "Verdana Ref", sans-serif}article{font-family:Merriweather !important}article h1{font-family:Merriweather !important}article li{font-family:Merriweather !important;line-height:1.6em}article li + li{margin-top:0.5em}article div.code{background:#f9f9f9;border:1px solid #d1d1d1;position:relative;border-radius:0.2em;margin:0.25em 0 0.75em 0;min-height:2.5em}article code{color:#005580;font-size:14px;white-space:pre}article a.hoogle > code{color:#0088cc;font-size:14px;white-space:pre;border:0}article .expand-code{display:none}article pre{overflow:auto;word-wrap:normal}article code.active{display:block;max-height:430px}article h1{font-size:31.5px}article h3{font-size:17.5px}article h4{font-size:14px}article h5,article h6{font-size:11.9px}article p{line-height:1.7em}article h1 code,article h2 code,article h3 code,article h4 code,article h5 code,article h6 code{font-size:inherit !important}article h1 a,article h2 a,article h3 a,article h4 a,article h5 a,article h6 a{color:#444}article h1 a:before,article h2 a:before,article h3 a:before,article h4 a:before,article h5 a:before,article h6 a:before{margin-left:-1.5em;padding-right:0.5em;font-family:fontawesome;content:"\f0c1";font-size:20px;color:#fff}article h1 a:hover,article h2 a:hover,article h3 a:hover,article h4 a:hover,article h5 a:hover,article h6 a:hover{text-decoration:none;color:#0088cc}article h1 a:hover:before,article h2 a:hover:before,article h3 a:hover:before,article h4 a:hover:before,article h5 a:hover:before,article h6 a:hover:before{color:#888}article h2{font-size:24.5px}article .popover{font-family:actor}article .popover h3.popover-title{font-family:actor !important;font-size:1em}article .popover .popover-content{width:15em;font-size:1em}article .controlsRun{display:block;padding:0.5em;text-align:right;background:#f2f2f2}article .controlsRun a.clone{margin-right:1em;font-family:actor;text-decoration:none}article .controlsRun a.clone span{padding-left:0.5em}article .controlsRun a.run,article .controlsRun a.reset,article .controlsRun a.show-code{font-size:16px}article .controlsRun a.run span,article .controlsRun a.reset span,article .controlsRun a.show-code span{display:none}article .controlsRun a.reset{margin-left:0.5em;color:#a2becc}article .controlsRun a.reset{display:none}article .controlsRun a.show-code{margin-right:0.75em}article .controlsRun a.run:hover,article .controlsRun a.show-code:hover{text-decoration:none}article .controlsRun a.show-code.active{color:#DC0D0D}article .controlsRun a[disabled]{color:#a2becc;cursor:default}article .collapse-area .collapse-header{background:#0088cc;color:#fff;padding:0.3em;cursor:pointer}article .collapse-area .hidden-toggle:before{content:"\f0d7";font-family:fontawesome;margin-right:0.5em;margin-left:0.25em}article .collapse-area .collapsed-files{overflow:auto;height:auto}article .collapse-area.collapse-disabled{display:none}article .collapse-area.collapse-area-off .collapsed-files{height:0px;overflow:hidden}article .collapse-area.collapse-area-off .hidden-toggle:before{content:"\f0da"}article .snippet-stdio .stdout{margin:0.5em;padding-right:2em;position:relative;word-wrap:break-word}article .snippet-stdio .stdin{-webkit-box-shadow:none;-moz-box-shadow:none;box-shadow:none;-webkit-transition:none;-moz-transition:none;-o-transition:none;transition:none;padding:0;margin:0}article .hoogle-results{margin:0.25em}article .hoogle-results .hoogle-inner{background:#fff;padding:0.5em;border-radius:3px;border:1px solid #ddd}article .hoogle-results .hoogle-inner .close-link{position:relative;float:right;margin-top:-0.5em;margin-right:-0.5em}article .snippet-output-errors{margin:1em 0.5em 0.5em 0.5em;position:relative}article .snippet-output-errors ul{list-style-type:none;margin:0;position:relative}article .snippet-output-errors pre{border:0;border-radius:0;padding:0.25em;margin:0;background:inherit;color:inherit}article .snippet-output-errors .close-link{color:inherit;background:inherit}article .snippet-editors{z-index:0;padding:0.25em}article .close-link{padding:0.5em;background:#eaeaea;border-bottom-left-radius:0.2em;border-top-right-radius:0.2em;position:absolute;top:0;right:0}article .close-link:hover{text-decoration:none}article .snippet-title{padding:0.5em 0 0 0.5em}article .snippet-title .snippet-icon{font-family:FontAwesome;display:inline-block;margin-right:0.5em}article .fullModuleCode + div .snippet-title{margin-top:0.5em;border-top:1px solid #ccc;padding-top:0.5em}article .CodeMirror{height:auto;min-height:1em;line-height:1.5em;background:transparent;font-size:14px !important}article .CodeMirror .highlighted{background:rgba(250, 210, 0, 0.45) !important}article .CodeMirror-widget{margin-left:2em;line-height:1.8}article .CodeMirror.collapsed{height:10em !important}article .editor .expander{text-align:center;display:block;padding:0.5em;text-decoration:none;border-bottom-left-radius:2px;border-bottom-right-radius:2px}article .editor .expander .ellipsis{background:white;border:1px solid #ccc;padding:0 0.5em;height:1em;line-height:0.5em;display:inline-block;color:#555;cursor:pointer}article .editor .expander .ellipsis:hover{background:#ddd;color:#333}article .web-runner{margin:0.5em;padding:0.5em;border:1px solid #ccc;border-radius:3px;background:#f5f5f5}article .web-runner iframe{border:0;margin-top:0.5em;margin-bottom:0;background:white}article .web-runner .controls i{cursor:pointer;color:#0088cc}article .web-runner .controls i:hover{color:#222}article .web-runner .controls i + i{margin-left:0.5em}article .web-runner .controls .icon-remove{float:right}article .hidden{display:block;visibility:visible}article .controlsShowHide{margin-bottom:0.5em}.tutorial-body{float:left !important}.tutorial-nav{float:right !important}@media (max-width: 979px) {.related-content{float:right}.author-name{display:block;margin-left:20px !important;padding-left:5px;margin-top:5px}article h1 a:hover:before,article h2 a:hover:before,article h3 a:hover:before,article h4 a:hover:before,article h5 a:hover:before,article h6 a:hover:before{color:#fff}.tutorial-nav{float:none !important;width:auto !important}.tutorial-body{float:none !important;width:auto !important}}@media (min-width: 980px) and (max-width: 1199px) {.navbar-search{display:none}.tutorial-nav{width:200px}.tutorial-body{width:720px}}#accountPage h1{margin:20px 0 0 0}#accountPage h2{font-size:20px}.group-contents .span8{float:left}.group-contents .span4{float:right}.container > .alert-block{margin-top:1em}.alert{color:#aa874a
}.social{margin-bottom:.8em;display:block}.social a{text-decoration:none}.social i{font-size:1.1em;color:#fff;padding:0.25em;border-radius:0.25em;width:1em;display:inline-block;text-align:center}.social i.icon-twitter{background:#0088cc}.social i.icon-facebook{background:#3b5998
  }.social i.icon-google-plus{background:#dd4b39
  }.well pre{background:#fcfcfc}.well h2{margin-top:0}code{color:#005580;font-size:14px;white-space:pre}</style><!--[if lt IE 8]><link rel="stylesheet" href="https://www.schoolofhaskell.com/static/design/css/ie7.css?etag=TSMl9x1V"><![endif]--><!--[if lt IE 9]><script src="//html5shiv.googlecode.com/svn/trunk/html5.js"></script><![endif]--></head><body class="not-logged-in" itemscope itemtype="http://schema.org/Product"><noscript><iframe src="//www.googletagmanager.com/ns.html?id=GTM-H62P" height="0" width="0" style="display:none;visibility:hidden"></iframe></noscript><script>(function(w,d,s,l,i){w[l]=w[l]||[];w[l].push({'gtm.start':new Date().getTime(),event:'gtm.js'});var f=d.getElementsByTagName(s)[0],j=d.createElement(s),dl=l!='dataLayer'?'&l='+l:'';j.async=true;j.src='//www.googletagmanager.com/gtm.js?id='+i+dl;f.parentNode.insertBefore(j,f);})(window,document,'script','dataLayer','GTM-H62P');</script><div class="navbar navbar-inverse navbar-static-top"><div class="navbar-inner"><div class="container"><a class="btn btn-navbar" type="button" data-toggle="collapse" data-target=".nav-collapse"><span class="icon-bar"></span>
<span class="icon-bar"></span>
<span class="icon-bar"></span>
</a>
<a class="brand" href="/"><img src="/static/img/haskell-logo-wide.png" title="School of Haskell">
</a>
<div class="nav-collapse collapse"><ul class="nav"><li><a href="https://www.schoolofhaskell.com/">School</a>
</li>
<li><a href="https://www.schoolofhaskell.com/user">Users</a>
</li>
</ul>
<form class="navbar-search pull-left" action="https://www.schoolofhaskell.com/search"><input class="search-query" type="text" placeholder="Search" name="search">
</form>
</div>
</div>
</div>
</div>
<div class="container breadcrumb-container"><div class="row"><div class="span12"><ul class="breadcrumb"><li><a href="https://www.schoolofhaskell.com/user/edwardk">Edward Kmett</a>
<span class="divider">/</span>
</li>
<li><a href="https://www.schoolofhaskell.com/user/edwardk/bound">Bound</a>
</li>
</ul>
</div>
</div>
</div>
<div class="container main-content"><div class="row"><div class="span12"><h1 itemprop="name">Bound</h1>
</div>
</div>
<div class="row"><div class="span7 meta"><p><span class="date"> 2 Dec 2015</span>
<span class="author"><a href="https://www.schoolofhaskell.com/user/edwardk">Edward Kmett</a>
</span>
<span class="view-edit-link pull-right"><a class="view-source-link" href="https://www.schoolofhaskell.com/tutorial-raw/4906/f941295a3eebf64a926799ca2aabafe14435b5f7">View Markdown source</a>
</span>
</p>
<p class="tags"></p>
</div>
</div>
<div class="row"><div class="span7 meta"><span class="sociallinksunicode social">
<a href="https://twitter.com/home?status=$url$" target="_blank"><i class="icon-twitter"></i></a>&nbsp;
<a href="http://www.facebook.com/sharer/sharer.php?u=$url$" target="_blank"><i class="icon-facebook"></i></a>&nbsp;
<a href="https://plus.google.com/share?url=$url$" target="_blank"><i class="icon-google-plus"></i></a></span>
</div>
</div>
<p class="alert">As of March 2020, School of Haskell has been switched to read-only mode.</p>
<div class="row" itemprop="desc"><div class="span4 tutorial-nav"><div class="related-content"><ul class="nav nav-tabs nav-stacked"><li><script>reddit_target='fpcomplete'</script>
<script src="https://redditstatic.s3.amazonaws.com/button/button1.js"></script>
</li>
<li><a href="https://www.schoolofhaskell.com/user/edwardk/phoas">Previous content: PHOAS For Free</a>
</li>
<li><a href="https://www.schoolofhaskell.com/user/edwardk/conquering-folds">Next content: Conquering Folds</a>
</li>
<li><a href="https://www.schoolofhaskell.com/user/edwardk">See all content by Edward Kmett</a>
</li>
</ul>

</div>
<div class="aside"><h3>Sections</h3>
<ul><li><a href="#what-is-in-a-name-">What is in a Name?</a><ul><li><a href="#na-ve-substitution">Naïve Substitution</a></li><li><a href="#the-barendregt-convention">The Barendregt Convention</a></li><li><a href="#higher-order-abstract-syntax--hoas-">Higher-Order Abstract Syntax (HOAS)</a></li><li><a href="#de-bruijn-indices">De Bruijn Indices</a></li><li><a href="#bird-and-paterson--part-1">Bird and Paterson, Part 1</a></li><li><a href="#attempt--1">Attempt #1</a></li><li><a href="#bird-and-paterson--part-2">Bird and Paterson, Part 2</a></li><li><a href="#bound--generalized-generalized-de-bruijn">Bound: Generalized Generalized De Bruijn</a></li><li><a href="#loose-ends">Loose Ends</a></li><li><a href="#bound-class">Bound.Class</a></li><li><a href="#what-s-next-">What&#39;s Next?</a></li></ul></li></ul></div>
</div>
<div class="span8 tutorial-body" data-environment="ghc-7.8-stable-14.09">
<article><p><a href="http://hackage.haskell.org/package/bound"><code>bound</code></a> provides a powerful but simple toolbox for dealing with capture-avoiding substitution in your code. It lets you use classes you already know: <code>Monad</code>, <code>Foldable</code> and <code>Traversable</code> to manipulate your syntax tree, and it factors out issues of name capture into a reusable monad transformer named <code>Scope</code>.</p><p>What I want to do today is help motivate the design of the <a href="http://hackage.haskell.org/package/bound"><code>bound</code></a> library by talking a bit about alternatives. This may serve, with a few digressions, as a bit of a crash course on capture-avoiding substitution, for those who don't deal with this issue on a day-to-day basis. This presentation is based on a talk I gave on <a href="http://hackage.haskell.org/package/bound"><code>bound</code></a>, but it has been upgraded to work with the School of Haskell tools for easier online consumption. In particular, I've tried to make the code fragments executable, and greatly expand on the content to cover things in greater depth. The <a href="http://www.slideshare.net/ekmett/bound-making-de-bruijn-succ-less">original slides</a> are also available, but were designed with me speaking over them in mind.</p><h1 id="what-is-in-a-name-"><a href="#what-is-in-a-name-">What is in a Name?</a></h1><p>We use names in lots of contexts, but any program that deals with names has to deal with a number of issues such as capture avoidance and deciding alpha equivalance... and others that will come up as we go.</p><p>If you go to write a syntax tree, the dumbest thing that can possibly work would be something like:</p><pre><code class="active haskell">type Name = String

data Exp 
  = Var Name 
  | App Exp Exp
  | Lam Name Exp
  deriving (Eq,Show,Read)

main = do
  print $ Var &quot;x&quot;
  print $ Lam &quot;x&quot; (Var &quot;x&quot;)
  print $ Lam &quot;x&quot; (Lam &quot;y&quot; (Var &quot;x&quot;))</code></pre><p>Two problems we want to address in almost any syntax tree are:</p><p>1.) Capture Avoidance</p><p>  Blindly substituting</p><pre><code class="haskell">Lam &quot;x&quot; (Var &quot;y&quot;) </code></pre><p>  into</p><pre><code class="haskell">Lam &quot;y&quot; (Var &quot;z&quot;)</code></pre><p>  for <code>z</code> would yield</p><pre><code class="haskell">Lam &quot;y&quot; (Lam &quot;x&quot; (Var &quot;y&quot;))</code></pre><p>  which now causes the formerly free variable <code>y</code> to now reference the <code>y</code> bound by the outer lambda.</p><p>2.) Alpha Equivalence</p><pre><code class="haskell">Lam &quot;x&quot; (Var &quot;x&quot;)</code></pre><p>  and</p><pre><code class="haskell">Lam &quot;y&quot; (Var &quot;y&quot;)</code></pre><p>  both mean the same thing and it'd be nice to be able to check this easily, make them hash the 
  same for common sub-expression elimination, etc.</p><p>There is a cottage industry of solutions to the naming problem:</p><p>e.g.</p><ul><li>Naïve substitution</li><li><a href="http://www.amazon.com/Calculus-Semantics-Studies-Foundations-Mathematics/dp/0444875085">The Barendregt convention</a></li><li><a href="http://en.wikipedia.org/wiki/Higher-order_abstract_syntax">HOAS</a></li><li>Weak HOAS/<a href="http://adam.chlipala.net/papers/PhoasICFP08/">PHOAS</a></li><li><a href="http://www.cs.ru.nl/~james/RESEARCH/haskell2004.pdf">&quot;I am not a Number: I am a Free Variable!&quot;</a></li><li><a href="http://www.chargueraud.org/research/2009/ln/main.pdf">The Locally Nameless Representation</a></li><li><a href="http://hackage.haskell.org/package/unbound">Unbound</a>, which mixes Barendregt with Locally Nameless</li><li>Locally Nameless Syntax with De Bruijn Indices</li></ul><p>I'm not looking to address all of these here, just a few, to showcase issues with the different points in the design space, and then to try to offer up a nice point in the design space (<code>bound</code>) that combines many of the advantages with few of the disadvantages.</p><h3 id="na-ve-substitution"><a href="#na-ve-substitution">Naïve Substitution</a></h3><p>Pros:</p><p>  1.) The syntax trees are pretty and easy to read.</p><p>  2.) Easy to use to get started</p><p>Cons:</p><p>  1.) It is easy even for experts to make mistakes.</p><p>  2.) Alpha equivalence checking is tedious</p><p>  3.) It is comically slow.</p><pre><code class="active haskell">import Data.List (union, span, (\\))

type Name = String

data Exp 
  = Var Name 
  | App Exp Exp
  | Lam Name Exp
  deriving (Eq,Show,Read)

-- show
freeVars :: Exp -&gt; [Name]
freeVars (Var x) = [x]
freeVars (App a b) = freeVars a `union` freeVars b 
freeVars (Lam n x) = freeVars x \\ [n]

allVars :: Exp -&gt; [Name]
allVars (Var x) = [x]
allVars (App a b) = allVars a `union` allVars b
allVars (Lam n x) = allVars x

subst :: Name -&gt; Exp -&gt; Exp -&gt; Exp
subst x s b = sub vs0 b where
  sub _ e@(Var v)
    | v == x = s
    | otherwise = e
  sub vs e@(Lam v e')
    | v == x = e
    | v `elem` fvs = Lam v' (sub (v':vs) e'')
    | otherwise = Lam v (sub vs e') where
    v' = newId vs
    e'' = subst v (Var v') e'
  sub vs (App f a) = sub vs f `App` sub vs a
  fvs = freeVars s
  vs0 = fvs `union` allVars b
  
newId :: [Name] -&gt; Name
newId vs = head (names \\ vs)

names :: [Name]
names = [ [i] | i &lt;- ['a'..'z']] ++ [i : show j | j &lt;- [1..], i &lt;- ['a'..'z'] ]
-- /show

-- show and we can see that this deals with capture avoidance
main = print $ subst &quot;z&quot; (Lam &quot;x&quot; (Var &quot;y&quot;)) (Lam &quot;y&quot; (Var &quot;z&quot;))
-- /show</code></pre><p>This code is adapted from Lennart Augustsson's excellent <a href="http://www.augustsson.net/Darcs/Lambda/top.pdf">λ-calculus cooked four ways</a>, which provides a nice crash course on name binding for evaluation.</p><p>[Edited <i>August 27 2013</i>: The code fragment above was updated based on a conversation with Marc André Ziegert to deal with an issue where in the <code>v <code>elem</code> fvs</code> case under <code>sub</code> it wasn't properly updating the set of <code>allVars b</code> when it introduced <code>v'</code> into scope. I told you naïve substitution was tricky!]</p><h3 id="the-barendregt-convention"><a href="#the-barendregt-convention">The Barendregt Convention</a></h3><p>This one is a serious contender. It is what GHC uses.</p><p>Pros:</p><p>  1.) The <a href="http://research.microsoft.com/en-us/um/people/simonpj/Papers/inlining/">Secrets of the Glasgow Haskell Compiler inliner</a> paper by <a href="http://www.youtube.com/watch?v=IDhz_mVcVCQ">Simon and Simon</a> describes a technique they call &quot;the Rapier,&quot; which can make this really fast.</p><p>Cons:</p><p>  1.) Easy even for experts to screw up.</p><p>  2.) Alpha equivalence is still tedious.</p><p>  3.) Need a globally unique variable supply. e.g. <a href="http://hackage.haskell.org/package/concurrent-supply">concurrent-supply</a></p><p>  4.) The obvious implementation technique chews through a scarily large number of variable IDs! Without the Rapier there is a lot of &quot;administrative&quot; renaming going on.</p><h3 id="higher-order-abstract-syntax--hoas-"><a href="#higher-order-abstract-syntax--hoas-">Higher-Order Abstract Syntax (HOAS)</a></h3><p>HOAS cheats and borrows substitution from the host language.</p><pre><code class="active haskell">-- show
data Exp a
  = Var a
  | Lam (Exp a -&gt; Exp a)
  | App (Exp a) (Exp a)
-- /show
main = putStrLn &quot;It typechecks, but a Show instance here is hard!&quot;</code></pre><p>Pros:</p><p>  1.) It provides ridiculously fast substitution by comparison to other techniques.</p><p>  2.) Meijer and Hutton's <a href="http://citeseerx.ist.psu.edu/viewdoc/download?doi=10.1.1.64.4921&amp;rep=rep1&amp;type=pdf">&quot;Bananas in Space&quot;</a>, Fegaras and Sheard's <a href="http://citeseer.ist.psu.edu/viewdoc/summary?doi=10.1.1.36.2763">&quot;Revisiting Catamorphisms over Datatypes with Embedded Functions (or, Programs from Outer Space)</a> and Weirich and Washburn's <a href="http://www.seas.upenn.edu/~sweirich/papers/itabox/icfp-published-version.pdf">&quot;Boxes Go Bananas: Encoding Higher-order Abstract Syntax with Parametric Polymorphism&quot;</a> each describe forms of catamorphism that can be used on these to work under binders.</p><p>Cons:</p><p>  1.) It doesn't work in theorem provers like Coq or Agda, because <code>Exp</code> occurs in both positive and negative position, causing it to fail the positivity check.</p><p>  2.) It is quite hard to work under binders. You often have to invert your control flow for passes and think &quot;inside out.&quot;</p><p>  3.) You have to deal with exotic terms that do bad things like inspect the expression they are given, so it is in some sense &quot;too big.&quot;</p><p>  4.) Alpha equivalence is still tedious.</p><p>  5.) In <a href="http://comonad.com/reader/2008/rotten-bananas/">&quot;Rotten Bananas&quot;</a> on <a href="http://comonad.com">comonad.com</a> I go through the issues with general recursion in this approach.</p><p>Variants such as Weak HOAS/<a href="http://adam.chlipala.net/papers/PhoasICFP08/">PHOAS</a>/<a href="http://www.seas.upenn.edu/~sweirich/papers/itabox/icfp-published-version.pdf">Weirich and Washburn</a> exist to address some of these issues (e.g. working in Coq/Agda, restoring positivity, ruling out exotic terms) at the expense of other problems (e.g. losing the notion of catamorphism or general recursion).</p><h3 id="de-bruijn-indices"><a href="#de-bruijn-indices">De Bruijn Indices</a></h3><blockquote><p>M'colleague Bob Atkey once memorably described the capacity to put up with De Bruijn indices as a Cylon
detector, the kind of reverse Turing Test that the humans in Battlestar Galactica invent, the better to
recognize one another by their common inadequacies. He had a point</p><p><i>-Conor McBride, &quot;I am not a number, I am a classy hack&quot;</i></p></blockquote><p>We can split variables into bound and free:</p><pre><code class="active haskell">-- show
{-# LANGUAGE DeriveFunctor, DeriveFoldable, DeriveTraversable #-}
import Data.Foldable
import Data.Traversable

data Exp a 
  = Free a
  | Bound !Int
  | App (Exp a) (Exp a)
  | Lam (Exp a)
  deriving (Eq,Show,Read,Functor,Foldable,Traversable)
-- /show
main = putStrLn &quot;It typechecks, but it is boring.&quot;</code></pre><p>We could define combinators to bind names and instantiate them</p><pre><code class="haskell">abstract :: Eq a =&gt; a -&gt; Exp a -&gt; Exp a
instantiate :: Exp a -&gt; Exp a -&gt; Exp a</code></pre><p>but let's adopt Conor McBride's less error prone convention instead:</p><pre><code class="active haskell">{-# LANGUAGE DeriveFunctor, DeriveFoldable, DeriveTraversable #-}
import Data.Foldable
import Data.Traversable
-- show
newtype Scope f a = Scope (f a) deriving (Eq,Show,Read,Functor,Foldable,Traversable)
data Exp a 
  = Free a
  | Bound !Int
  | App (Exp a) (Exp a)
  | Lam (Scope Exp a)
-- /show
  deriving (Eq,Show,Read,Functor,Foldable,Traversable)
main = putStrLn &quot;It typechecks, and is even slightly interesting.&quot;</code></pre><p>With Conor's convention, <code>abstract</code> and <code>instantiate</code> actually have useful types that prevent you from doing bad things. Renaming <code>Bound</code> to <code>B</code> and <code>Free</code> to <code>F</code>, then adapting the approach he takes in his excellent functional pearl with James McKinna <a href="http://www.cs.ru.nl/~james/RESEARCH/haskell2004.pdf">&quot;I am not a Number -- I am a Free Variable&quot;</a> to our running example to try to keep Conor's somewhat absurd sense of humor intact we get:</p><pre><code class="active haskell">{-# LANGUAGE DeriveFunctor, DeriveFoldable, DeriveTraversable #-}
import Data.Foldable
import Data.Traversable

newtype Scope f a = Scope (f a) deriving (Eq,Show,Read,Functor,Foldable,Traversable)
data Exp a 
  = F a
  | B !Int
  | App (Exp a) (Exp a)
  | Lam (Scope Exp a)
  deriving (Eq,Show,Read,Functor,Foldable,Traversable)
-- show
abstract :: Eq a =&gt; a -&gt; Exp a -&gt; Scope Exp a
abstract me expr = Scope (letmeB 0 expr) where
  letmeB this (F you) | you == me = B this
                      | otherwise = F you
  letmeB this (B that)            = B that
  letmeB this (App fun arg)       = letmeB this fun `App` letmeB this arg
  letmeB this (Lam (Scope body))  = Lam $ Scope $ letmeB (succ this) body

instantiate :: Exp a -&gt; Scope Exp a -&gt; Exp a
instantiate what (Scope body) = what'sB 0 body where
  what'sB this (B that) | this == that = what
                        | otherwise    = B that
  what'sB this (F you)                 = F you
  what'sB this (App fun arg)           = what'sB this fun `App` what'sB this arg
  what'sB this (Lam (Scope body))      = Lam $ Scope $ what'sB (succ this) body
-- /show
main = putStrLn &quot;It typechecks, and is even slightly interesting.&quot;</code></pre><p>We could even make a monad that does capture avoiding substitution for <code>Exp</code>, but it is an awkward one-off experience.</p><p>Pros:</p><p>  1.) <code>Scope</code>, <code>abstract</code>, and <code>instantiate</code> made it harder to screw up walking under binders.</p><p>  2.) Alpha equivalence is just <code>(==)</code> due to the power of De Bruijn indices.</p><p>  3.) We <i>can</i> make a <code>Monad</code> for <code>Exp</code> that does <i>capture avoiding</i> substitution.</p><p>  4.) We can use <code>Traversable</code> and <code>Foldable</code> to find free variables and close terms.</p><pre><code>Using these we can define combinators like</code></pre><pre><code class="active haskell">import Data.Foldable as Foldable
import Data.Traversable
  
-- show
closed :: Traversable f =&gt; f a -&gt; Maybe (f b)
closed = traverse (const Nothing)

isClosed :: Foldable f =&gt; f a -&gt; Bool
isClosed = Foldable.all (const False)
-- /show
    
main = putStrLn &quot;The types check out.&quot;</code></pre><pre><code>to check for closed terms, and we can use `Exp Void` directly as a closed term.</code></pre><p>Cons:</p><p>  1.) We <code>succ</code> a lot in <code>letmeB</code> and <code>what'sB</code>.</p><p>  2.) Illegal terms such as <code>Lam (Scope (B 2))</code> still exist.</p><p>  3.) We have to define one-off versions of <code>abstract</code> and <code>instantiate</code> for each expression type we come up with. This means this is a design pattern, not a library.</p><p>  4.) The <code>Monad</code> for <code>Exp</code> is similarly a one-off deal.</p><h3 id="bird-and-paterson--part-1"><a href="#bird-and-paterson--part-1">Bird and Paterson, Part 1</a></h3><p>We can turn to Bird and Paterson's encoding of De Bruijn indices in terms of polymorphic recursion from the first half of <a href="http://www.cs.uwyo.edu/~jlc/courses/5000_fall_08/debruijn_as_nested_datatype.pdf">De Bruijn notation as a nested datatype</a>.</p><pre><code class="active haskell">{-# LANGUAGE DeriveFunctor, DeriveFoldable, DeriveTraversable #-}
import Data.Foldable
import Data.Traversable

-- show
data Exp a
  = Var a
  | App (Exp a) (Exp a)
  | Lam (Exp (Maybe a))
  deriving (Functor, Foldable, Traversable)
  
instance Monad Exp where
  return = Var
  Var a &gt;&gt;= f = f a
  App l r &gt;&gt;= f = App (l &gt;&gt;= f) (r &gt;&gt;= f)
  Lam xs &gt;&gt;= f = undefined -- go for it!
-- /show

main = print &quot;Did you finish (&gt;&gt;=)?&quot;</code></pre><p>Pros:</p><p>  1.) This eliminates the illegal terms from Con #2 above once and for all, and we can write a one-off <code>Monad</code> for it.</p><p>  2.) We have the <code>Maybe</code> to mark our extra variables, so we can't forget to deal with our bound term at any point in our recursion.</p><p>Cons:</p><p>  1.) We still <code>succ</code> a lot, except now we call <code>succ</code> <code>Just</code>.</p><p>  2.) We still need to write one-off <code>abstract</code> and <code>instantiate</code> combinators.</p><p>  3.) The <code>Monad</code> for <code>Exp</code> is a one-off deal.</p><p>  4.) They lean somewhat unnecessarily on <code>RankNTypes</code> to manipulate these expressions in terms of their folds, when the <code>Monad</code> and other classes can all be defined within the confines of <a href="http://www.haskell.org/onlinereport/">Haskell 98</a>.</p><h3 id="attempt--1"><a href="#attempt--1">Attempt #1</a></h3><p>We can modify Bird and Paterson's approach in a few ways, to steal some of the desirable properties from McBride's &quot;I am not a Number; I'm a Free Variable&quot; by noticing that Scope forms a Monad transformer!</p><pre><code class="active haskell">{-# LANGUAGE DeriveFunctor, DeriveFoldable, DeriveTraversable #-}
import Control.Monad
import Control.Monad.Trans
import Data.Foldable
import Data.Functor
import Data.Traversable

-- show
newtype Scope f a = Scope { runScope :: f (Maybe a) }
  deriving (Functor, Foldable, Traversable)

instance Monad f =&gt; Monad (Scope f) where
  return = Scope . return . Just
  Scope m &gt;&gt;= f = undefined -- go for it!

instance MonadTrans Scope where
  lift = Scope . liftM Just
-- /show

main = print &quot;Scope scopes out, but did you finish (&gt;&gt;=)?&quot;</code></pre><p>The astute observer will recognize that this form of <code>Scope</code> is just <a href="http://hackage.haskell.org/packages/archive/transformers/0.3.0.0/doc/html/Control-Monad-Trans-Maybe.html"><code>MaybeT</code></a> from <a href="http://hackage.haskell.org/package/transformers"><code>transformers</code></a>.</p><p>We can finally even define <code>abstract</code> and <code>instantiate</code> from Conor's approach once and for all, completely independently of our expression type. Sadly, they find themselves now devoid of humor.</p><pre><code class="haskell">abstract :: (Functor f, Eq a) =&gt; a -&gt; f a -&gt; Scope f a
abstract x xs = Scope (fmap go xs) where
  go y = y &lt;$ guard (x /= y)
  
instantiate :: Monad f =&gt; f a -&gt; Scope f a -&gt; f a
instantiate x (Scope xs) = xs &gt;&gt;= go where
  go Nothing = x
  go (Just y) = return y</code></pre><p>This is starting to feel like a library, rather than design pattern.</p><p>Moreover, with that in hand, we can revisit the definition of <code>Exp</code> and define our <code>Monad</code> by borrowing from <code>Scope</code>'s expression-type-agnostic definition.</p><pre><code class="active haskell">{-# LANGUAGE DeriveFunctor, DeriveFoldable, DeriveTraversable #-}
import Control.Monad
import Control.Monad.Trans
import Data.Foldable
import Data.Functor
import Data.Traversable

newtype Scope f a = Scope { runScope :: f (Maybe a) }
  deriving (Functor, Foldable, Traversable)

instance Monad f =&gt; Monad (Scope f) where
  return = Scope . return . Just
  Scope m &gt;&gt;= f = Scope $ m &gt;&gt;= maybe (return Nothing) (runScope . f)
 
instance MonadTrans Scope where
  lift = Scope . liftM Just

abstract :: (Functor f, Eq a) =&gt; a -&gt; f a -&gt; Scope f a
abstract x xs = Scope (fmap go xs) where
  go y = y &lt;$ guard (x /= y)
  
instantiate :: Monad f =&gt; f a -&gt; Scope f a -&gt; f a
instantiate x (Scope xs) = xs &gt;&gt;= go where
  go Nothing  = x
  go (Just y) = return y

-- show
data Exp a
  = Var a
  | App (Exp a) (Exp a)
  | Lam (Scope Exp a)
  deriving (Functor, Foldable, Traversable)
  
instance Monad Exp where
  return = Var
  Var a &gt;&gt;= f    = f a
  App l r &gt;&gt;= f  = App (l &gt;&gt;= f) (r &gt;&gt;= f)
  Lam body &gt;&gt;= f = Lam (body &gt;&gt;= lift . f)
-- /show

main = putStrLn &quot;Now we're getting somewhere!&quot;</code></pre><p>Pros:</p><p>  1.) We were able to factor out <code>Scope</code>, <code>abstract</code> and <code>instantiate</code> into code we can write once and package up.</p><p>  2.) We've gained the benefits of McBride's conventions, as <code>abstract</code> and <code>instantiate</code> are really nice to reason about.</p><p>Cons:</p><p>  1.) This is still going to <code>succ</code> just as much as our earlier De Bruijn solutions. Using <code>lift</code> to embed into <code>Scope</code> has to traverse all of the leaves to mangle them with a <code>Just</code>.</p><p>  2.) <code>Eq</code> <code>Ord</code>, <code>Show</code>, and <code>Read</code> are now non-trivial to write due to polymorphic recursion.</p><h3 id="bird-and-paterson--part-2"><a href="#bird-and-paterson--part-2">Bird and Paterson, Part 2</a></h3><p>Bird and Paterson did write the other half of their paper for a reason, though. In the other half they showed we can encode a <i>generalized</i> De Bruijn index notion using a different polymorphic recursion pattern.</p><pre><code class="haskell">data Exp a
  = Var a
  | App (Exp a) (Exp a)
  | Lam (Exp (Maybe (Exp a)))</code></pre><p>The astute observer will now note that this is looking a bit less like <code>MaybeT</code>. So what happened?</p><p>Normally when you work with De Bruijn indices you <code>succ</code> only the variables down at the leaves.</p><p>What Bird and Paterson's generalized De Bruijn form allows you to do is <code>Just</code> née <code>succ</code> whole trees at a time!</p><p>If we adopt my <code>MonadTrans</code> variant of McBride's <code>Scope</code> as before, and generalize Bird and Paterson's approach, sticking to Haskell 98. We get very close to the final solution actually encoded in <code>bound</code>.</p><pre><code class="active haskell">{-# LANGUAGE DeriveFunctor, DeriveFoldable, DeriveTraversable #-}
import Control.Monad
import Control.Monad.Trans
import Data.Foldable
import Data.Functor
import Data.Maybe
import Data.Traversable

-- show
newtype Scope f a = Scope { runScope :: f (Maybe (f a)) }
  deriving (Functor, Foldable, Traversable)
  
instance Monad f =&gt; Monad (Scope f) where
  return = Scope . return . Just . return
  Scope e &gt;&gt;= f = Scope $ e &gt;&gt;= \v -&gt; case v of
    Nothing -&gt; return Nothing
    Just ea -&gt; ea &gt;&gt;= runScope . f
    
instance MonadTrans Scope where
  lift = Scope . return . Just
  
abstract :: (Monad f, Eq a) =&gt; a -&gt; f a -&gt; Scope f a
abstract a e = Scope (liftM k e) where
  k b = return b &lt;$ guard (a /= b)

instantiate :: Monad f =&gt; f a -&gt; Scope f a -&gt; f a
instantiate k (Scope e) = e &gt;&gt;= fromMaybe k
-- /show
main = putStrLn &quot;Almost there!&quot;</code></pre><p>The key difference realized by Bird and Paterson's design is that now, lifting an expression that does not have our bound term in it into our new <code>Scope</code>, no longer requires touching every leaf in the expression. In fact it is <code>O(1)</code>!</p><p><code>Scope</code> is still a <code>Monad</code> transformer, but it is a new one!</p><p>This is a salvageable representation, but I want to take one more step before we turn it into a library, before we talk about Pros and Cons.</p><h3 id="bound--generalized-generalized-de-bruijn"><a href="#bound--generalized-generalized-de-bruijn">Bound: Generalized Generalized De Bruijn</a></h3><p>Often we need to deal with simultaneous substitution of several variables. e.g. all of the variables bound by a pattern, all of the variables bound by a lambda, many variables bound by recursive let</p><p><code>De Bruijn</code> even as generalized above still only lets me <code>abstract</code> or <code>instantiate</code> a single variable at a time.</p><p>What I want relates to <code>EitherT</code> as the above monad relates to <code>MaybeT</code>.</p><p>Finally, I'm going to alpha rename <code>Either</code>, because really when we <i>do</i> finally get to show you these syntax trees, they are going to be bad enough without meaningless <code>Left</code> and <code>Right</code> names cluttering up my tree. Let's use <code>B</code> for bound (pronounced <code>zero</code>) and <code>F</code> for free (pronounced <code>succ</code>) in homage to Conor's conventions above.</p><pre><code class="active haskell">{-# LANGUAGE DeriveFunctor, DeriveFoldable, DeriveTraversable #-}
import Control.Monad
import Control.Monad.Trans
import Data.Foldable
import Data.Functor
import Data.Maybe
import Data.Traversable

-- show Var
data Var b a 
  = B b 
  | F a 
  deriving (Eq,Ord,Show,Read,Functor,Foldable,Traversable)

instance Monad (Var b) where
  return = F
  F a &gt;&gt;= f = f a
  B b &gt;&gt;= _ = B b
-- /show

-- show Scope
newtype Scope b f a = Scope { runScope :: f (Var b (f a)) }
  deriving (Functor,Foldable,Traversable)
  
instance Monad f =&gt; Monad (Scope b f) where
  return = Scope . return . F . return
  Scope e &gt;&gt;= f = Scope $ e &gt;&gt;= \v -&gt; case v of
    B b  -&gt; return (B b)
    F ea -&gt; ea &gt;&gt;= runScope . f
    
instance MonadTrans (Scope b) where
  lift = Scope . return . F
-- /show

-- show Abstraction and Instantiation
abstract :: Monad f =&gt; (a -&gt; Maybe b) -&gt; f a -&gt; Scope b f a
abstract f e = Scope (liftM k e) where
  k y = case f y of
    Just z  -&gt; B z
    Nothing -&gt; F (return y)

instantiate :: Monad f =&gt; (b -&gt; f a) -&gt; Scope b f a -&gt; f a
instantiate k e = runScope e &gt;&gt;= \v -&gt; case v of
  B b -&gt; k b
  F a -&gt; a
-- /show

main = putStrLn &quot;It typechecks, therefore it must be correct!&quot;</code></pre><p>From here on out we can actually use the real <code>bound</code> library.</p><p>Pros:</p><p>  1.) Because we can now <code>succ</code>/<code>F</code> a whole tree, we don't have to pay the usual De Bruijn performance tax. 
      We get <code>O(1)</code> lifting, and when we <code>instantiate</code> we can skip past the whole <code>F</code>'d tree.</p><p>  2.) We use <code>Foldable</code> and <code>Traversable</code> to extract information about free variables and do
      variable -&gt; variable substitution.</p><p>  3.) We can use the <code>MonadTrans</code> instance for <code>Scope b</code> to facilitate the construction of <code>Exp</code>. 
      We do this so often that we turn the <code>&gt;&gt;= lift . f</code> idiom into a combinator (<code>&gt;&gt;&gt;=</code>) 
      that we'll talk about further when we talk about patterns.</p><pre><code>This is a complete example syntax tree example that supports the use of `Foldable`/`Traversable`/`Monad`
to extract information about the free variables, and do capture-avoiding substitution!</code></pre><pre><code class="active haskell">-- /show
{-# LANGUAGE DeriveFunctor, DeriveFoldable, DeriveTraversable #-}
import Data.Foldable
import Data.Traversable
import Bound

data Exp a
 = Var a
 | App (Exp a) (Exp a)
 | Lam (Scope () Exp a)
 | Let [Scope Int Exp a] (Scope Int Exp a)
 deriving (Functor,Foldable,Traversable)
  
instance Monad Exp where
  return = Var
  Var a &gt;&gt;= f = f a
  App l r &gt;&gt;= f = App (l &gt;&gt;= f) (r &gt;&gt;= f)
  Lam s &gt;&gt;= f = Lam (s &gt;&gt;&gt;= f)
  Let xs b &gt;&gt;= f = Let (map (&gt;&gt;&gt;= f) xs) (b &gt;&gt;&gt;= f)
-- /show
main = putStrLn &quot;That's it!&quot;</code></pre><p>  4.) We can now deal with simultaneous substitution. To make things interesting in the example above we've extended the example with a simultaneous binding for all of the variables in a recursive let!</p><p>Cons:</p><p>  1.) I still haven't shown you how to get <code>Eq</code>, <code>Ord</code>, <code>Read</code>, <code>Show</code> such that <code>Eq</code> and <code>Ord</code> respect alpha-equivalence.</p><h3 id="loose-ends"><a href="#loose-ends">Loose Ends</a></h3><p>To work around the polymorphic recursion in <code>Scope</code>, we turn to my fairly boring and generically named <code>prelude-extras</code> package. This package provides higher-rank versions of typeclasses from the <code>Prelude</code>.</p><p>For example</p><pre><code class="haskell">class Eq1 t where
  (==#) :: Eq a =&gt; t a -&gt; t a -&gt; Bool
  (/=#) :: Eq a =&gt; t a -&gt; t a -&gt; Bool</code></pre><p>along with <code>Eq2</code>, <code>Ord1</code>, <code>Ord2</code>, <code>Show1</code>, <code>Show2</code>, <code>Read1</code> and <code>Read2</code>.</p><p>I told you it was boring.</p><p>There has also been some discussion on the libraries mailing list of randomly renaming these things and putting (a subset?) of these in <code>transformers</code> to permit Haskell 98 <code>Show</code> instances things like <code>IdentityT</code> and <code>WriterT</code>.</p><p>Now, what we can do is use the fact that these classes have default definitions and the instances for <code>Scope</code> are defined in terms of <code>Eq1</code>, <code>Ord1</code>, <code>Show1</code>, and <code>Read1</code> for the base data type, to obtain the final solution!</p><pre><code class="active haskell">-- show
{-# LANGUAGE DeriveFunctor, DeriveFoldable, DeriveTraversable #-}
import Bound
import Control.Applicative
import Control.Monad
import Data.Foldable
import Data.Traversable
import Prelude.Extras

data Exp a 
  = Var a 
  | App (Exp a) (Exp a) 
  | Lam (Scope () Exp a)
  deriving (Eq,Ord,Show,Read,Functor,Foldable,Traversable)

instance Eq1 Exp
instance Ord1 Exp
instance Show1 Exp
instance Read1 Exp
instance Applicative Exp where 
  pure = Var
  (&lt;*&gt;) = ap

instance Monad Exp where
  return = Var
  Var a   &gt;&gt;= f = f a
  App x y &gt;&gt;= f = App (x &gt;&gt;= f) (y &gt;&gt;= f)
  Lam e   &gt;&gt;= f = Lam (e &gt;&gt;&gt;= f)
-- /show
whnf :: Exp a -&gt; Exp a
whnf (App f a) = case whnf f of
  Lam b -&gt; whnf (instantiate1 a b)
  f'    -&gt; App f' a
whnf e = e

lam :: Eq a =&gt; a -&gt; Exp a -&gt; Exp a
lam v b = Lam (abstract1 v b)

nf :: Exp a -&gt; Exp a
nf e@Var{}   = e
nf (Lam b) = Lam $ toScope $ nf $ fromScope b
nf (App f a) = case whnf f of
  Lam b -&gt; nf (instantiate1 a b)
  f' -&gt; nf f' `App` nf a

main = putStrLn &quot;It compiles&quot;</code></pre><p>and we can finally show syntax tree terms again.</p><p>The instances of <code>Eq</code> and <code>Ord</code> for <code>Scope</code> in particular are careful to compare only up to alpha-equivalence by quotienting out the placement of any internal <code>F</code> levels in the tree as if they'd all been pushed out to the leaves.</p><p>We can use <code>abstract1</code> and <code>instantiate1</code>, which are analogous to the <code>abstract</code> and <code>instantiate</code> we were using before we generalized our generalized De Bruijn index representation to define things like smart constructors</p><pre><code class="haskell">lam :: Eq a =&gt; a -&gt; Exp a -&gt; Exp a
lam v b = Lam (abstract1 v b)</code></pre><p>and evaluation strategies such as computing weak head normal form:</p><pre><code class="haskell">whnf :: Exp a -&gt; Exp a
whnf (App f a) = case whnf f of
  Lam b -&gt; whnf (instantiate1 a b)
  f'    -&gt; App f' a
whnf e = e</code></pre><p>One thing the <code>f (Maybe a)</code> representation was good for was for walking under binders. <code>bound</code> offers the round trip through this representation as <code>fromScope</code> and <code>toScope</code>. Now, if we need to walk under a lambda, we can!</p><pre><code class="haskell">nf :: Exp a -&gt; Exp a
nf e@V{}   = e
nf (Lam b) = Lam $ toScope $ nf $ fromScope b
nf (App f a) = case whnf f of
  Lam b -&gt; nf (instantiate1 a b)
  f' -&gt; nf f' `App` nf a</code></pre><h3 id="bound-class"><a href="#bound-class">Bound.Class</a></h3><p>Finally, it is worth commenting on the generality of <code>(&gt;&gt;&gt;=)</code> as it points to the class for which this package is named.</p><p>In addition to <code>Scope</code> and <code>Var</code>, the <code>bound</code> package provides the <code>Bound</code> class:</p><pre><code class="haskell">class Bound t where
  (&gt;&gt;&gt;=) :: Monad f =&gt; t f a -&gt; (a -&gt; f b) -&gt; t f b
  default (&gt;&gt;&gt;=) :: (MonadTrans t, Monad f) =&gt; t f a -&gt; (a -&gt; f b) -&gt; t f b
  m &gt;&gt;&gt;= f = m &gt;&gt;= lift . f</code></pre><p>An instance of <code>Bound</code> is required to form a left module over all monads. That is to say it should satisfy the following two laws that were identified by Andrea Vezzosi:</p><p>1.) The first such law is analogous to the first monad law:</p><pre><code class="haskell">m &gt;&gt;&gt;= return ≡ m</code></pre><p>2.) The second such law is an associativity law:</p><pre><code class="haskell">m &gt;&gt;&gt;= (λ x → k x &gt;&gt;= h) ≡ (m &gt;&gt;&gt;= k) &gt;&gt;&gt;= h</code></pre><p>Trivially, any valid <code>MonadTrans</code> instance satisfies these laws, hence the default definition.</p><p>This extra flexibility is used in the <a href="https://github.com/ekmett/bound"><code>bound</code></a> <a href="https://github.com/ekmett/bound/tree/master/examples"><code>examples/</code></a> folder to deal with complex pattern matching or other binding/telescoping structures.</p><h3 id="what-s-next-"><a href="#what-s-next-">What's Next?</a></h3><p>You've just taken a crash course on name capture and been shown how <code>bound</code> deals with common isses and makes it easy to work with De Bruijn indices without ever thinking about De Bruijn indices.</p><p>Hopefully, you want to learn more. Perhaps after a slight break to let this settle. If so, I can offer you three further avenues for exploration off the top of my head.</p><p>First, I'd like to note that there are a few middlingly-complex examples in the <a href="https://github.com/ekmett/bound"><code>bound</code></a> <a href="https://github.com/ekmett/bound/tree/master/examples"><code>examples/</code></a> folder to explore and use as a template. Notably:</p><p>1.) <a href="https://github.com/ekmett/bound/blob/master/examples/Simple.hs"><code>Simple.hs</code></a> continues in the vein 
    we've taken here but fleshes out recursive let and sticks to Haskell 98 religiously.
2.) 
  <a href="https://github.com/ekmett/bound/blob/master/examples/Deriving.hs"><code>Deriving.hs</code></a> takes over from 
  where we're leaving off with here and uses</p><pre><code class="haskell">{-# LANGUAGE DeriveFunctor, DeriveFoldable, DeriveTraversable #-}</code></pre><p>  to reduce boilerplate, and importantly adds pattern matching. This showcases the need for the <code>Bound</code>
  class for dealing with patterns, case alternatives, and similar module structures.</p><p>3.) <a href="https://github.com/ekmett/bound/blob/master/examples/Overkill.hs"><code>Overkill.hs</code></a> offers a trip down the rabbit hole of type safety. It shows how strong the type guarantees <i>can</i> get, by using custom kinds to index into its patterns and improves the safety of pattern matching and let binding over and above the <code>Deriving.hs</code> approach, but at the expense of a great deal more code!</p><p>Second, it is also possible to derive a higher order version of <code>bound</code> that can deal with strongly typed EDSLs as well. I've included a worked example version of it here under a spoiler tag simply for completeness, including an example of what could be turned into a network serializable EDSL replete with local variable bindings and lambdas, but it isn't for the faint of heart. It is also probably buggy and is completely untested. If you fix any issues with it, please feel free to email me!</p><div class="hidden"><pre><code class="active haskell">
{-# LANGUAGE GADTs, Rank2Types, KindSignatures, ScopedTypeVariables, TypeOperators, DataKinds, PolyKinds, MultiParamTypeClasses, FlexibleInstances, TypeFamilies, DoRec, ExtendedDefaultRules #-}

import Control.Applicative
import Control.Category
import Control.Comonad
import Control.Monad.Fix
import Control.Monad (ap)
import Data.Functor.Identity
import Data.Typeable
import Data.Monoid
import Data.Unique
import System.IO.Unsafe
import Unsafe.Coerce
import Prelude hiding ((.),id)

infixl 1 &gt;&gt;&gt;-, &gt;&gt;-

type Nat f g = forall x. f x -&gt; g x

class HFunctor t where
  hmap :: Nat f g -&gt; Nat (t f) (t g)

class HFunctor t =&gt; HTraversable t where
  htraverse :: Applicative m =&gt; (forall x. f x -&gt; m (g x)) -&gt; t f a -&gt; m (t g a)

class HFunctor t =&gt; HMonad t where
  hreturn :: Nat f (t f)
  (&gt;&gt;-)   :: t f a -&gt; Nat f (t g) -&gt; t g a

infixr 1 -&lt;&lt;
(-&lt;&lt;) :: HMonad t =&gt; Nat f (t g) -&gt; Nat (t f) (t g)
f -&lt;&lt; m = m &gt;&gt;- f

class HBound s where
  (&gt;&gt;&gt;-) :: HMonad t =&gt; s t f a -&gt; Nat f (t g) -&gt; s t g a

class HMonadTrans s where
  hlift :: HMonad t =&gt; Nat (t f) (s t f)

data Var b f a where
  B :: b a -&gt; Var b f a
  F :: f a -&gt; Var b f a

instance HFunctor (Var b) where
  hmap _ (B b) = B b
  hmap f (F a) = F (f a)

instance HTraversable (Var b) where
  htraverse _ (B b) = pure (B b)
  htraverse f (F a) = F &lt;$&gt; f a

instance HMonad (Var b) where
  hreturn   = F
  B b &gt;&gt;- _ = B b
  F a &gt;&gt;- f = f a

newtype Scope b t f a = Scope { unscope :: t (Var b (t f)) a }

instance HFunctor t =&gt; HFunctor (Scope b t) where
  hmap f (Scope b) = Scope (hmap (hmap (hmap f)) b)

instance HTraversable t =&gt; HTraversable (Scope b t) where
  htraverse f (Scope b) = Scope &lt;$&gt; htraverse (htraverse (htraverse f)) b

instance HMonad t =&gt; HMonad (Scope b t) where
  hreturn = Scope . hreturn . F . hreturn
  Scope e &gt;&gt;- f  = Scope $ e &gt;&gt;- \v -&gt; case v of
    B b -&gt; hreturn (B b)
    F ea -&gt; ea &gt;&gt;- unscope . f

instance HMonadTrans (Scope b) where
  hlift = Scope . hreturn . F

instance HBound (Scope b) where
  Scope m &gt;&gt;&gt;- f = Scope (hmap (hmap (&gt;&gt;- f)) m)

data Equal a b where
   Refl :: Equal a a

instance Category Equal where
  id = Refl
  Refl . Refl = Refl

abstract :: HMonad t =&gt; (forall x. f x -&gt; Maybe (b x)) -&gt; Nat (t f) (Scope b t f)
abstract f = Scope . hmap (\y -&gt; case f y of
  Just b -&gt; B b
  Nothing -&gt; F (hreturn y))

instantiate :: HMonad t =&gt; Nat b (t f) -&gt; Nat (Scope b t f) (t f)
instantiate k (Scope e) = e &gt;&gt;- \v -&gt; case v of
  B b -&gt; k b
  F a -&gt; a

data Ix :: [*] -&gt; * -&gt; * where
  Z :: Ix (a ': as) a
  S :: Ix as b -&gt; Ix (a ': as) b

data Vec :: (* -&gt; *) -&gt; [*] -&gt; * where
  HNil :: Vec f '[]
  (:::) :: f b -&gt; Vec f bs -&gt; Vec f (b ': bs)

infixr 5 :++, :::

type family (:++) (as :: [*]) (bs :: [*]) :: [*]
type instance '[] :++ bs = bs
type instance (a ': as) :++ bs = a ': (as :++ bs)

happend :: Vec f as -&gt; Vec f bs -&gt; Vec f (as :++ bs)
happend HNil bs = bs
happend (a ::: as) bs = a ::: happend as bs

hsingleton :: f a -&gt; Vec f '[a]
hsingleton x = x ::: HNil

instance HFunctor Vec where
  hmap _ HNil = HNil
  hmap f (x ::: xs) = f x ::: hmap f xs

instance HTraversable Vec where
  htraverse _ HNil = pure HNil
  htraverse f (x ::: xs) = (:::) &lt;$&gt; f x &lt;*&gt; htraverse f xs

class EqF f where
  (==?) :: f a -&gt; f b -&gt; Maybe (Equal a b)

data Lit t where
  Integer :: Integer -&gt; Lit Integer
  Double  :: Double -&gt; Lit Double
  String :: String -&gt; Lit String

instance EqF Lit where
  Integer a ==? Integer b = Just Refl
  ...

value :: Lit a -&gt; a
value (Integer i) = i
value (Double d) = d
value (String s) = s

class Literal a where
  literal :: a -&gt; Lit a

instance Literal Integer where
  literal = Integer

instance Literal String where
  literal = String

data Remote :: (* -&gt; *) -&gt; * -&gt; * where
  Var :: f a -&gt; Remote f a
  Lit :: Lit a -&gt; Remote f a
  Lam :: Scope (Equal b) Remote f a -&gt; Remote f (b -&gt; a)
  Let :: Vec (Scope (Ix bs) Remote f) bs -&gt; Scope (Ix bs) Remote f a -&gt; Remote f a
  Ap  :: Remote f (a -&gt; b) -&gt; Remote f a -&gt; Remote f b

lam_ :: EqF f =&gt; f a -&gt; Remote f b -&gt; Remote f (a -&gt; b)
lam_ v f = Lam (abstract (v ==?) f)
 
lit :: Literal a =&gt; a -&gt; Remote f a
lit = Lit . literal

instance HFunctor Remote where
  hmap f (Var a)    = Var (f a)
  hmap _ (Lit t)    = Lit t
  hmap f (Lam b)    = Lam (hmap f b)
  hmap f (Let bs b) = Let (hmap (hmap f) bs) (hmap f b)
  hmap f (Ap x y)   = Ap (hmap f x) (hmap f y)

instance HTraversable Remote where
  htraverse f (Var a)    = Var &lt;$&gt; f a
  htraverse _ (Lit t)    = pure $ Lit t
  htraverse f (Lam b)    = Lam &lt;$&gt; htraverse f b
  htraverse f (Let bs b) = Let &lt;$&gt; htraverse (htraverse f) bs &lt;*&gt; htraverse f b
  htraverse f (Ap x y)   = Ap &lt;$&gt; htraverse f x &lt;*&gt; htraverse f y

data MyF a where
  Mean :: MyF (Double -&gt; Double)

instance HMonad Remote where
  hreturn        = Var
  Var a    &gt;&gt;- f = f a
  Lit t    &gt;&gt;- _ = Lit t
  Lam b    &gt;&gt;- f = Lam (b &gt;&gt;&gt;- f)
  Let bs b &gt;&gt;- f = Let (hmap (&gt;&gt;&gt;- f) bs) (b &gt;&gt;&gt;- f)
  Ap x y   &gt;&gt;- f = Ap (x &gt;&gt;- f) (y &gt;&gt;- f)

(!) :: Vec f v -&gt; Ix v a -&gt; f a
(b ::: _)  ! Z   = b
(_ ::: bs) ! S n = bs ! n

eval :: Remote Identity a -&gt; a
eval (Var w) = extract w
eval (Lit i) = value i
eval (Lam b) = \a -&gt; eval (instantiate (\Refl -&gt; Var (Identity a)) b)
eval (Let bs b) = eval (instantiate (vs !) b) where vs = hmap (instantiate (vs !)) bs
eval (Ap x y) = eval x (eval y)

closed :: HTraversable t =&gt; t f a -&gt; Maybe (t g a)
closed = htraverse (const Nothing)

newtype V (a :: *) = V Unique

instance EqF V where
  V a ==? V b
    | a == b    = Just (unsafeCoerce Refl)
    | otherwise = Nothing

lam :: (Remote V a -&gt; Remote V b) -&gt; Remote V (a -&gt; b)
lam f = unsafePerformIO $ do
  x &lt;- fmap V newUnique
  return $ Lam $ abstract (x ==?) $ f $ Var x

data Binding a = V a := Remote V a

rhs :: Binding a -&gt; Remote V a
rhs (_ := a) = a

data Bindings = forall bs. Bindings (Vec Binding bs)

elemIndex :: Vec Binding bs -&gt; V a -&gt; Maybe (Ix bs a)
elemIndex HNil              _ = Nothing
elemIndex ((x := r) ::: xs) y = case x ==? y of
  Just Refl -&gt; Just Z
  Nothing   -&gt; S &lt;$&gt; elemIndex xs y

instance Monoid Bindings where
  mempty = Bindings HNil
  Bindings xs `mappend` Bindings ys = Bindings (happend xs ys)

-- Allow the use of DoRec to define let statements

newtype Def a = Def { runDef :: IO (a, Bindings) }

instance Functor Def where
  fmap f (Def m) = Def $ do
    (a,w) &lt;- m
    return (f a, w)

instance Applicative Def where
  pure = return
  (&lt;*&gt;) = ap

instance Monad Def where
  return a = Def $ return (a, mempty)
  Def m &gt;&gt;= f = Def $ do
    (a, xs) &lt;- m
    (b, ys) &lt;- runDef (f a)
    return (b, xs &lt;&gt; ys)

instance MonadFix Def where
  mfix m = Def $ mfix $ \ ~(a, _) -&gt; runDef (m a)

def :: Remote V a -&gt; Def (Remote V a)
def v@Var{} = Def $ return (v, mempty) -- this thing already has a name
def r = Def $ do
  v &lt;- fmap V newUnique
  return (Var v, Bindings (hsingleton (v := r)))

let_ :: Def (Remote V a) -&gt; Remote V a
let_ (Def m) = unsafePerformIO $ do
    (r, Bindings bs) &lt;- m
    return $ Let (hmap (abstract (elemIndex bs) . rhs) bs)
                 (abstract (elemIndex bs) r)
</code></pre></div><p>Third, for a much larger &quot;industrial scale&quot; example you can explore our work-in-progress <a href="https://github.com/ermine-language/ermine">compiler for Ermine</a> that uses <a href="https://github.com/ekmett/bound"><code>bound</code></a> (and <a href="https://github.com/ekmett/lens"><code>lens</code></a>) extensively for manipulating its <a href="http://ermine-language.github.io/ermine/Ermine-Syntax-Term.html"><code>Term</code></a> language, <a href="http://ermine-language.github.io/ermine/Ermine-Syntax-Type.html"><code>Type</code></a> system, <a href="http://ermine-language.github.io/ermine/Ermine-Syntax-Kind.html"><code>Kind</code></a> system and the <a href="http://ermine-language.github.io/ermine/Ermine-Syntax-Core.html"><code>Core</code></a> language it spits out as a witness during type checking.</p><p>Happy Hacking!</p><p>-<a href="mailto:ekmett@gmail.com">Edward Kmett</a></p><p>August 19 2013</p><p>P.S. Apparently the proper Dutch convention with names is to use <a href="http://en.wikipedia.org/wiki/Nicolaas_Govert_de_Bruijn">Nicolaas Govert de Bruijn</a> when writing out a full name, but to capitalize De Bruijn when using the surname in isolation.</p></article>

<div id="disqus_thread"><script>var disqus_shortname = "edwardk"; (function() { var dsq = document.createElement('script'); dsq.type = 'text/javascript'; dsq.async = true; dsq.src = 'https://' + disqus_shortname + '.disqus.com/embed.js'; (document.getElementsByTagName('head')[0] || document.getElementsByTagName('body')[0]).appendChild(dsq); })();</script>
<noscript>Please enable JavaScript to view the <a href="https://disqus.com/?ref_noscript">comments powered by Disqus.</a></noscript>
<a class="dsq-brlink" href="https://disqus.com">comments powered by <span class="logo-disqus">Disqus</span></a></a>
</div>

</div>
</div>
<div class="row article-footer"><div class="span7 meta"><span class="sociallinksunicode social">
<a href="https://twitter.com/home?status=$url$" target="_blank"><i class="icon-twitter"></i></a>&nbsp;
<a href="http://www.facebook.com/sharer/sharer.php?u=$url$" target="_blank"><i class="icon-facebook"></i></a>&nbsp;
<a href="https://plus.google.com/share?url=$url$" target="_blank"><i class="icon-google-plus"></i></a></span>
</div>
</div>
</div>
<div class="footer"><div class="container"><div class="row"><div class="span12"><ul class="menu"><li><a href="https://www.schoolofhaskell.com/">School</a>
</li>
<li><a href="https://www.schoolofhaskell.com/user">Users</a>
</li>

<li><a class="brand" href="https://fpcomplete.com">Sponsored by:
<img src="/static/img/logo.png" title="FPComplete">
</a>
</li>
</ul>
</div>
</div>
<div class="row"><div class="span12"></div>
</div>
</div>
</div>
<link rel="stylesheet" type="text/css" href="/static/bootstrap/css/bootstrap-responsive.css" />
<script src="//ajax.googleapis.com/ajax/libs/jquery/1.8.2/jquery.min.js"></script><script src="https://www.schoolofhaskell.com/static/combined/Y-3k9_tW.js"></script><script>$(function(){
  $("a.logout").click(function(){
    var form = $("<form method='post'/>");
    form.attr("action", $(this).attr("href"));
    $("body").append(form);
    form.submit();
    return false;
  });
});

$(function(){
    $('.sociallinksunicode a').each(function(){
        $(this).attr('href',$(this).attr('href').replace('$url$',document.location));
    });
});

$(function(){
  // If we need to select a user/select a password then show the
  // details confirmation form, otherwise enable the getting started
  // section.
  if($('.alert-block a').text().match(/(selected a username|change your password)/)) {
    $("#confirm-details").show();
    $("#confirm-details #inputUsername").focus();
  } else {
    $("#get-started").removeClass('disabled').addClass('enabled');
  }

  // For lack of a plain HTML validation story this will do.
  $('.welcome-page').each(function(){
    $('form').submit(check);
  });
  function check(){
    setTimeout(check,500);
    var error = false;
    $('.error').removeClass('error');
    $('#enter-details input').each(function(){
      if($(this).val() == '') {
        $(this).parent().parent().addClass('error');
        error = true;
      }
    });
    if(error) return false;
    if($('[name=password]').val() == $('[name=confirm]').val()) {
      $('#confirm').removeClass('error');
      return true;
    } else {
      $('#confirm').addClass('error');
      return false;
    }
  }
});

$(function(){
  // When scrolling over a snippet editor, activate the "clone in IDE" popovers
  $('article').each(function(){
    var n = 100;
    var wait = 10000;
    function check(){
      if($('.clone').length == 0) {
        setTimeout(check,n);
        return;
      }
      var activated = $.cookie('clone-popover');
      if (activated) return;
      var activate = false;
      $('.controlsRun:onScreen').each(function(){
        $.cookie('clone-popover', 'true', { expires: 30, path: '/' });
        activate = true;
        return false;
      });
      if(activate) {
          $('.clone').each(function(){
              // The bootstrap API is rather unreasonable to say the
              // least.
              next = $(this).parent();
              var title = next.attr('title');
              next.attr('data-content',$(this).attr('data-content'));
              next.attr('title',$(this).attr('title'));
              next.popover('show',{
                  title: $(this).attr('title')
              });
              next.attr('title',title);
          });
          setTimeout(function(){
              $('.clone').each(function(){
                  $(this).parent().popover('hide');
              });
          },wait);
      } else {
        setTimeout(check,n);
      }
    }
    setTimeout(check,n);
  });
});

// onScreen jQuery plugin v0.2.1
// (c) 2011-2013 Ben Pickles
//
// http://benpickles.github.com/onScreen
//
// Released under MIT license.
(function(a){a.expr[":"].onScreen=function(b){var c=a(window),d=c.scrollTop(),e=c.height(),f=d+e,g=a(b),h=g.offset().top,i=g.height(),j=h+i;return h>=d&&h<f||j>d&&j<=f||i>e&&h<=d&&j>=f}})(jQuery);

/*!
 * jQuery Cookie Plugin v1.1
 * https://github.com/carhartl/jquery-cookie
 *
 * Copyright 2011, Klaus Hartl
 * Dual licensed under the MIT or GPL Version 2 licenses.
 * http://www.opensource.org/licenses/mit-license.php
 * http://www.opensource.org/licenses/GPL-2.0
 */
(function($, document) {

  var pluses = /\+/g;
  function raw(s) {
    return s;
  }
  function decoded(s) {
    return decodeURIComponent(s.replace(pluses, ' '));
  }

  $.cookie = function(key, value, options) {

    // key and at least value given, set cookie...
    if (arguments.length > 1 && (!/Object/.test(Object.prototype.toString.call(value)) || value == null)) {
      options = $.extend({}, $.cookie.defaults, options);

      if (value == null) {
	options.expires = -1;
      }

      if (typeof options.expires === 'number') {
	var days = options.expires, t = options.expires = new Date();
	t.setDate(t.getDate() + days);
      }

      value = String(value);

      return (document.cookie = [
	encodeURIComponent(key), '=', options.raw ? value : encodeURIComponent(value),
	options.expires ? '; expires=' + options.expires.toUTCString() : '', // use expires attribute, max-age is not supported by IE
	options.path    ? '; path=' + options.path : '',
	options.domain  ? '; domain=' + options.domain : '',
	options.secure  ? '; secure' : ''
      ].join(''));
    }

    // key and possibly options given, get cookie...
    options = value || $.cookie.defaults || {};
    var decode = options.raw ? raw : decoded;
    var cookies = document.cookie.split('; ');
    for (var i = 0, parts; (parts = cookies[i] && cookies[i].split('=')); i++) {
      if (decode(parts.shift()) === key) {
	return decode(parts.join('='));
      }
    }
    return null;
  };
  $.cookie.defaults = {};})(jQuery, document);
</script><!--[if lt IE 10]><script src="https://www.schoolofhaskell.com/static/design/js/ie.js?etag=ayV2DuJ0"></script><![endif]--><script>if(!window.location.href.match(/localhost/)){var track = '/tutorial/user/edwardk/bound';
window._gaq = [['_setAccount','UA-36928035-1'],track != ''? ['_trackPageview',track] : ['_trackPageview']
,['_trackPageLoadTime']];window._gaq.push(['_setCustomVar',1,'Section','School of Haskell',3]);
window._gaq.push(['_setCustomVar',2,'User Type','Visitor',1]);
(function() {
  var ga = document.createElement('script'); ga.type = 'text/javascript'; ga.async = true;
  ga.src = ('https:' == document.location.protocol ? 'https://ssl' : 'http://www') + '.google-analytics.com/ga.js';
  var s = document.getElementsByTagName('script')[0]; s.parentNode.insertBefore(ga, s);

})();}</script>
<!-- Prompt IE 6 users to install Chrome Frame. Remove this if you want to support IE 6.  chromium.org/developers/how-tos/chrome-frame-getting-started --><!--[if lt IE 7 ]><script src="//ajax.googleapis.com/ajax/libs/chrome-frame/1.0.3/CFInstall.min.js"></script><script>window.attachEvent('onload',function(){CFInstall.check({mode:'overlay'})})</script><![endif]--></body></html>