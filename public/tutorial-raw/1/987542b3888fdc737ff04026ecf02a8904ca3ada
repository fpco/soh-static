


        



The first step with any new technology is getting it running. The goal of this chapter is to get you started with a simple Yesod application, and cover some of the basic concepts and terminology.




    



## Hello World


Let's get this book started properly: a simple web page that says Hello World:



```haskell active web
{-# LANGUAGE TypeFamilies, QuasiQuotes, MultiParamTypeClasses,
             TemplateHaskell, OverloadedStrings #-}
import Yesod

data HelloWorld = HelloWorld

mkYesod "HelloWorld" [parseRoutes|
/ HomeR GET
|]

instance Yesod HelloWorld

getHomeR :: Handler RepHtml
getHomeR = defaultLayout [whamlet|Hello World!|]

main :: IO ()
main = warpEnv HelloWorld

```



If you save that code in <code>helloworld.hs</code> and run it with <code>runhaskell helloworld.hs</code>, you'll get a web server running on port 3000. If you point your browser to [http://localhost:3000](http://localhost:3000), you'll get the following HTML:



```
<!DOCTYPE html>
<html><head><title></title></head><body>Hello World!</body></html>
```




        



We'll refer back to this example through the rest of the chapter.



## Routing


Like most modern web frameworks, Yesod follows a [front controller pattern](http://en.wikipedia.org/wiki/Front_Controller_pattern). This means that every request to a Yesod application enters at the same point and is routed from there. As a contrast, in systems like PHP and ASP you usually create a number of different files, and the web server automatically directs requests to the relevant file.




        



In addition, Yesod uses a declarative style for specifying routes. In our example above, this looked like:




        



```haskell
mkYesod "HelloWorld" [parseRoutes|
/ HomeR GET
|]

```




        



<code>mkYesod</code> is a Template Haskell function, and <code>parseRoutes</code> is a QuasiQuoter.




        



In English, all this means is: <q>In the HelloWorld application, create one route. I'd like to call it <code>HomeR</code>, it should listen for requests to <code>/</code> (the root of the application), and should answer <code>GET</code> requests</q>. We call <code>HomeR</code> a __resource__, which is where the "R" suffix comes from.




        
        



The R suffix on resource names is simply convention, but it's a fairly universally followed convention. It makes it just a bit easier to read and understand code.




        



The <code>mkYesod</code> TH function generates quite a bit of code here: a route data type, a dispatch function, and a render function. We'll look at this in more detail in the routing chapter. But by using the <code>-ddump-splices</code> GHC option, we can get an immediate look at the generated code. A much cleaned up version of it is:




        



```haskell active
{-# LANGUAGE TypeFamilies, MultiParamTypeClasses, OverloadedStrings #-}
import qualified Yesod.Routes.Dispatch
import Yesod.Routes.Class
import Yesod.Core

data HelloWorld = HelloWorld

-- show
instance RenderRoute HelloWorld where
  data Route HelloWorld = HomeR
    deriving (Show, Eq, Read)
  renderRoute HomeR = ([], [])

instance YesodDispatch HelloWorld HelloWorld where
    yesodDispatch logger master sub toMaster app404 app405 method pieces =
        case dispatch pieces of
            Just f -> f
                logger
                master
                sub
                toMaster
                app404
                app405
                method
            Nothing -> app404
      where
        dispatch = Yesod.Routes.Dispatch.toDispatch
            [ Yesod.Routes.Dispatch.Route [] False onHome
            ]
        onHome [] = Just $ \logger master sub toMaster _app404 app405 method ->
            case method of
                "GET" -> yesodRunner
                    logger
                    (fmap chooseRep getHomeR)
                    master
                    sub
                    (Just HomeR)
                    toMaster
                _ -> app405 HomeR
-- /show

getHomeR = return ()

main :: IO ()
main = return ()

```




        



Some of that will likely not make sense yet. In particular, the implementation of <code>yesodDispatch</code> is a bit hairy to accomodate different dispatch approaches and fit the model necessary for our high-performance routing structures. However, the <code>RenderRoute</code> implementation with its associated data type should already give you a good feel for what's going on under the surface.




        
        



If you are interested in learning more about the code generated by Yesod, or are interested in using Yesod without code generation, you may wish to check out the [yesod-pure](http://hackage.haskell.org/package/yesod-pure) project. There's [a good overview blog post](http://www.yesodweb.com/blog/2012/10/yesod-pure) with an explanation of how to replace generated code with hand-written code.




        
        



## Handler function



        



So we have a route named <code>HomeR</code>, and it responds to <code>GET</code> requests. How do you define your response? You write a __handler function__. Yesod follows a standard naming scheme for these functions: it's the lower case method name (e.g., <code>GET</code> becomes <code>get</code>) followed by the route name. In this case, the function name would be <code>getHomeR</code>.



Most of the code you write in Yesod lives in handler functions. This is where you process user input, perform database queries and create responses. In our simple example, we create a response using the <code>defaultLayout</code> function. This function wraps up the content it's given in your site's template. By default, it produces an HTML file with a doctype and <code>html</code>, <code>head</code> and <code>body</code> tags. As we'll see in the Yesod typeclass chapter, this function can be overridden to do much more.



In our example, we pass <code>[whamlet|Hello World!|]</code> to <code>defaultLayout</code>. <code>whamlet</code> is another quasi-quoter. In this case, it converts Hamlet syntax into a Widget. Hamlet is the default HTML templating engine in Yesod. Together with its siblings Cassius, Lucius and Julius, you can create HTML, CSS and Javascript in a fully type-safe and compile-time-checked manner. We'll see much more about this in the Shakespeare chapter.




        



Widgets are another cornerstone of Yesod. They allow you to create modular components of a site consisting of HTML, CSS and Javascript and reuse them throughout your site. We'll get into more detail on them in the widgets chapter.



## The Foundation


The word "HelloWorld" shows up a number of times in our example. Every Yesod application has a __foundation__ datatype. This datatype must be an instance of the Yesod typeclass, which provides a central place for declaring a number of different settings controlling the execution of our application.




        



In our case, this datatype is pretty boring: it doesn't contain any information. Nonetheless, the foundation is central to how our example runs: it ties together the routes with the instance declaration and lets it all be run. We'll see throughout this book that the foundation pops up in a whole bunch of places.




        



But foundations don't have to be boring: they can be used to store lots of useful information, usually stuff that needs to be initialized at program launch and used throughout. Some very common examples are:




        



* A database connection pool.



* Settings loaded from a config file.



* An HTTP connection manager.



By the way, the word Yesod (יסוד) means *foundation* in Hebrew.



## Running


Once again we mention <code>HelloWorld</code> in our main function. Our foundation contains all the information we need to route and respond to requests in our application; now we just need to convert it into something that can run. A useful function for this in Yesod is <code>warpDebug</code>, which runs the Warp webserver with debug output enabled on the specified port (here, it's 3000).



One of the features of Yesod is that you aren't tied down to a single deployment strategy. Yesod is built on top of the Web Application Interface (WAI), allowing it to run on FastCGI, SCGI, Warp, or even as a desktop application using the Webkit library. We'll discuss some of these options in the deployment chapter. And at the end of this chapter, we will explain the development server.




        



Warp is the premiere deployment option for Yesod. It is a lightweight, highly efficient web server developed specifically for hosting Yesod. It is also used outside of Yesod for other Haskell development (both framework and non-framework applications), as well as a standard file server in a number of production environments.



## Resources and type-safe URLs


In our hello world, we defined just a single resource (<code>HomeR</code>). A web application is usually much more exciting with more than one page on it. Let's take a look:



```haskell active web
{-# LANGUAGE TypeFamilies, QuasiQuotes, MultiParamTypeClasses,
             TemplateHaskell, OverloadedStrings #-}
import Yesod

data Links = Links

mkYesod "Links" [parseRoutes|
/ HomeR GET
/page1 Page1R GET
/page2 Page2R GET
|]

instance Yesod Links

getHomeR  = defaultLayout [whamlet|<a href=@{Page1R}>Go to page 1!|]
getPage1R = defaultLayout [whamlet|<a href=@{Page2R}>Go to page 2!|]
getPage2R = defaultLayout [whamlet|<a href=@{HomeR}>Go home!|]

main = warpEnv Links

```



Overall, this is very similar to Hello World. Our foundation is now <code>Links</code> instead of <code>HelloWorld</code>, and in addition to the <code>HomeR</code> resource, we've added <code>Page1R</code> and <code>Page2R</code>. As such, we've also added two more handler functions: <code>getPage1R</code> and <code>getPage2R</code>.



The only truly new feature is inside the <code>whamlet</code> quasi-quotation. We'll delve into syntax in the Shakespeare chapter, but we can see that:



```
<a href=@{Page1R}>Go to page 1!
```



creates a link to the <code>Page1R</code> resource. The important thing to note here is that <code>Page1R</code> is a data constructor. By making each resource a data constructor, we have a feature called __type-safe URLs__. Instead of splicing together strings to create URLs, we simply create a plain old Haskell value. By using at-sign interpolation (<code>@{...}</code>), Yesod automatically renders those values to textual URLs before sending things off to the user. We can see how this is implemented by looking again at the <code>-ddump-splices</code> output:




        



```haskell active
{-# LANGUAGE TypeFamilies, OverloadedStrings #-}
import Yesod.Core

data Links = Links

-- show
instance RenderRoute Links where
    data Route Links = HomeR | Page1R | Page2R
      deriving (Show, Eq, Read)

    renderRoute HomeR  = ([], [])
    renderRoute Page1R = (["page1"], [])
    renderRoute Page2R = (["page2"], [])
-- /show

main :: IO ()
main = return ()

```




        



In the <code>Route</code> associated type for <code>Links</code>, we have additional constructors for <code>Page1R</code> and <code>Page2R</code>. We also now have a better glimpse of the return values for <code>renderRoute</code>. The first part of the tuple gives the __path pieces__ for the given route. The second part gives the query string parameters; for almost all use cases, this will be an empty list.




        



It's hard to over-estimate the value of type-safe URLs. They give you a huge amount of flexibility and robustness when developing your application. You can move URLs around at will without ever breaking links. In the routing chapter, we'll see that routes can take parameters, such as a blog entry URL taking the blog post ID.




        



Let's say you want to switch from routing on the numerical post ID to a year/month/slug setup. In a traditional web framework, you would need to go through every single reference to your blog post route and update appropriately. If you miss one, you'll have 404s at runtime. In Yesod, all you do is update your route and compile: GHC will pinpoint every single line of code that needs to be corrected.



## The scaffolded site



    



Installing Yesod will give you both the Yesod library, as well as a <code>yesod</code> executable. This executable accepts a few commands, but the first one you'll want to be acquainted with is <code>yesod init</code>. It will ask you some questions, and then generate a folder containing the default __scaffolded site__. Inside that folder, you can run <code>cabal install
        --only-dependencies</code> to build any extra dependencies (such as your database backends), and then <code>yesod devel</code> to run your site.




    



The scaffolded site gives you a lot of best practices out of the box, setting up files and dependencies in a time-tested approach used by most production Yesod sites. However, all this convenience can get in the way of actually learning Yesod. Therefore, most of this book will avoid the scaffolding tool, and instead deal directly with Yesod as a library.




    



We will cover the structure of the scaffolded site in more detail later.




  



## Development server


One of the advantages interpreted languages have over compiled languages is fast prototyping: you save changes to a file and hit refresh. If we want to make any changes to our Yesod apps above, we'll need to call <code>runhaskell</code> from scratch, which can be a bit tedious.



Fortunately, there's a solution to this: <code>yesod
                devel</code> automatically rebuilds and reloads your code for you. This can be a great way to develop your Yesod projects, and when you're ready to move to production, you still get to compile down to incredibly efficient code. The Yesod scaffolding automatically sets things up for you. This gives you the best of both worlds: rapid prototyping **and** fast production code.



It's a little bit more involved to set up your code to be used by <code>yesod devel</code>, so our examples will just use <code>warpDebug</code>. But when you're ready to make your real-world applications, <code>yesod devel</code> will be waiting for you.



## Summary


Every Yesod application is built around a foundation datatype. We associate some resources with that datatype and define some handler functions, and Yesod handles all of the routing. These resources are also data constructors, which lets us have type-safe URLs.



By being built on top of WAI, Yesod applications can run with a number of different backends. <code>warpDebug</code> is an easy way to get started, as it's included with Yesod. For rapid development, <code>yesod devel</code> is a good choice. And when you're ready to move to production, you have Warp as a high-performance option.




        



When developing in Yesod, we get a number of choices for coding style: quasi-quotation or external files, <code>warpDebug</code> or <code>yesod
                devel</code>, and so on. The examples in this book will tend towards using the choices that are easiest to copy-and-paste, but the more powerful options will be available when you start building real Yesod applications.

